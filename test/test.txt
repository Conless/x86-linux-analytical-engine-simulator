N 0 90
N 1 1           # n1 = 1;
N 2 2           # n2 = 2;
N 3 2           # n3 = divisor;
N 4 2           # n4 = quotient;
N 5 0           # n5 = remainder;
N 6 0           # n6 = 0;

# for n3 == 2;

/               # n0 / n3;
L 0
L 3
S' 4            # n4 = n0 / n3;
S 5             # n5 = n0 % n3;

-               # if (n6 < n5) skip;
L 6
L 5
?F 6
                # else
P 3             # printf("%d\n", n3);
+
L 4
L 6
S 0             # n0 = n4;
B 15            # turn back;

-               # n3 = 1 (then plus 2)
L 3
L 1
S 3

# for n3 > 2;

+
L 3
L 2
S 3

/               # n0 / n3;
L 0
L 3
S' 4            # n4 = n0 / n3;
S 5             # n5 = n0 % n3;

-               # if (n0 % n3 != 0) skip;
L 6
L 5
?F 6
                # else
P 3             # printf("%d\n", n3);
+
L 4
L 6
S 0             # n0 = n4;
B 15            # turn back;

-
L 3
L 4
?B 23           # while (n3 < n4) repeat;

-
L 0
L 2
?F 1

P 0